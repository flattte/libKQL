// MORE GEBERAL INFORMATION ABOUT INCIDENT
// check password spray [countries number of tries time span]
let lookback = 1h;
union SigninLogs, AADNonInteractiveUserSignInLogs
| where TimeGenerated between (ago(lookback) .. now()) //changing now() for a more specfic time might sometimes be necessary
| where UserPrincipalName contains "szewczy"
| extend Reason = case( // pls add ResultTypes if you find any and update commit / create a pull request
    ResultType =~ "0", "Successful Logon",
    ResultType =~ "50125", "User authentication was blocked because they need to provide password reset information. Their next interactive sign in will ask them for this, which the app should trigger next.",
    ResultType =~ "50126" , "Invalid username or password or Invalid on-premises username or password",
    strcat('Unknown reason substatus: ', ResultType)
)
| summarize make_set(Reason), make_set(ResourceDisplayName), make_set(IPAddress),
    startTime=min(TimeGenerated), endTime=max(TimeGenerated), make_set(ResultType),
    triesCount=count(), span = max(TimeGenerated) - min(TimeGenerated) by Location


// VALIDATE IF INCIDENT WAS SUCCESFUL
// check if valid logins happened
let lookback = 1h;
union SigninLogs, AADNonInteractiveUserSignInLogs
| where TimeGenerated between (ago(lookback) .. now()) // changing now() for a more specfic time might sometimes be necessary
| where UserPrincipalName contains "UPN" // insert UserPrincipalName
| where ResultType has_any ("0", "50140") // this are valid logonds, add ResultTypes custom for this incident if needed
| summarize startTime=min(TimeGenerated), endTime=max(TimeGenerated), make_set(ResultType),
     triesCount=count(), span = max(TimeGenerated) - min(TimeGenerated) by Location
