// multiple failures followed by a success | monkaS (ʘ ʖ̯ ʘ)
let failure = 3;
let success = 1;
let timeRange = ago(1d);
let authenticationWindow = 20m;
SigninLogs
| where TimeGenerated >= timeRange  
| extend OS = DeviceDetail.operatingSystem
| extend City = tostring(LocationDetails.city)
| where AppDisplayName contains "Azure Portal"
| extend FailureOrSuccess = iff(ResultType in ("0", "50125", "50140"), "Success", "Failure")
| summarize StartTimeUtc = min(TimeGenerated), EndTimeUtc = max(TimeGenerated), 
    makeset(IPAddress), makeset(OS), makeset(City), makeset(ResultType),
    FailureCount = countif(FailureOrSuccess=="Failure"),
    SuccessCount = countif(FailureOrSuccess=="Success") 
    by bin(TimeGenerated, authenticationWindow), UserDisplayName, UserPrincipalName
| where FailureCount>=failure and SuccessCount>=success
